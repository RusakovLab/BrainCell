
{ load_file("../../AltRunControl/Exported/alt_stdrun.hoc") }
loadIfNotDeclared("AltRunControlWidget", "../../AltRunControl/Exported/AltRunControlWidget.hoc")

{ makeSureDeclared("altRunControlWidget", "objref %s", "%s = new AltRunControlWidget()") }


objref ecsManagerMainWidget, exportManagerMainWidget

begintemplate ManageAndRunWidget

    public show, showBioManagerHandler, showSynManagerHandler, showGapJuncManagerHandler, showECSManagerHandler, showAltRunControlHandler, onNanoReseedOrUpdateHandler, dismissHandler
    
    external isAstrocyteOrNeuron
    external mechManagerMainWidget, synManagerMainWidget, gapJuncManagerMainWidget, ecsManagerMainWidget, exportManagerMainWidget, altRunControlWidget
    external mmAllComps, gjmAllGapJuncs, gapJuncGroups, smAllSyns, synGroup
    external mwh, mmIcrHelper
    external inhomAndStochLibrary
    external getMaxDist, dismissIfNotNil, codeContractViolation
    
    objref mainBox, this
    
    enumBioGjSynEcs = -1
    
    
    proc init() {
        enumBioGjSynEcs = $1
    }
    
    proc show() {
        mainBox = new VBox()
        mainBox.intercept(1)
        {
            xpanel("")
            if (enumBioGjSynEcs == 0) {
                xbutton("Show biophys manager", "showBioManagerHandler()")
            } else if (enumBioGjSynEcs == 1) {
                xbutton("Show gap junc manager", "showGapJuncManagerHandler()")
            } else if (enumBioGjSynEcs == 2) {
                xbutton("Show synapse manager", "showSynManagerHandler()")
            } else if (enumBioGjSynEcs == 3) {
                xbutton("Show ECS manager", "showECSManagerHandler()")
            } else {
                codeContractViolation()
            }
            xbutton("Show Alt Run Control", "showAltRunControlHandler()")
            xpanel()
        }
        mainBox.intercept(0)
        mainBox.dismiss_action("dismissHandler()")
        mainBox.map("Manage and Run", 25, 575, -1, -1)
    }
    
    proc showBioManagerHandler() { localobj nil
        dismissIfNotNil(exportManagerMainWidget)
        dismissIfNotNil(mechManagerMainWidget)
        
        if (mmAllComps.count() == 0) {
            mwh.showWarningBox("Cannot open the biophys manager because there are no compartments.")
            return
        }
        
        mmIcrHelper.rescanNow()
        
        if (mechManagerMainWidget == nil) {
            execute("mechManagerMainWidget = new MechManagerMainWidget()", this)
        }
        
        mechManagerMainWidget.show()
        mechManagerMainWidget.mechOpSelectHandler()
    }
    
    proc showGapJuncManagerHandler() { local wasNil, isDisconnected, maxDist localobj nil
        dismissIfNotNil(exportManagerMainWidget)
        dismissIfNotNil(gapJuncManagerMainWidget)
        
        wasNil = (gapJuncManagerMainWidget == nil)
        
        if (wasNil) {
            mwh.startIntercepting() // For the warnings shown by pyObj.ms_warnIfNoRecordForGapJunc
            // !!! tiny BUG: the warnings are not shown again when user closes GapJuncManagerMainWidget and opens it again
            
            execute("gapJuncManagerMainWidget = new GapJuncManagerMainWidget()", this)
        }
        
        isDisconnected = getMaxDist(&maxDist)
        if (isDisconnected) {
            mwh.showWarningBox("Cannot edit inhomogeneity in gap junctions", "because at least one gap junction target section doesn't have a topological connection with the distance centre.")
            
            // !! overkill here: we don't need to prevent opening the widget because user might wanted to do smth unrelated to inhomogeneity
            
            if (wasNil) {
                mwh.endIntercepting()
            }
            return
        }
        
        gapJuncManagerMainWidget.show(maxDist)
        
        if (wasNil) {
            mwh.endIntercepting()
        }
    }
    
    proc showSynManagerHandler() { local isDisconnected, xMin, xMax localobj nil
        dismissIfNotNil(exportManagerMainWidget)
        dismissIfNotNil(synManagerMainWidget)
        
        if (smAllSyns.count() == 0) {
            mwh.showWarningBox("Cannot open the synapse manager because there are no synapses.")
            return
        }
        
        if (synManagerMainWidget == nil) {
            execute("synManagerMainWidget = new SynManagerMainWidget()", this)
        }
        
        isDisconnected = synGroup.getDistRange(&xMin, &xMax)
        if (isDisconnected) {
            mwh.showWarningBox("Cannot edit inhomogeneity in synapses", "because at least one synapse target section doesn't have a topological connection with the distance centre.")
            
            // !! overkill here: we don't need to prevent opening the widget because user might wanted to do smth unrelated to inhomogeneity
            return
        }
        
        synManagerMainWidget.show(xMin, xMax)
    }
    
    proc showECSManagerHandler() { localobj nil
        dismissIfNotNil(exportManagerMainWidget)
        dismissIfNotNil(ecsManagerMainWidget)
        
        if (ecsManagerMainWidget == nil) {
            execute("ecsManagerMainWidget = new ExtracellularManagerMainWidget()", this)
        }
        
        ecsManagerMainWidget.show()
    }
    
    proc showAltRunControlHandler() {
        dismissIfNotNil(altRunControlWidget)
        altRunControlWidget.show()
    }
    
    proc onNanoReseedOrUpdateHandler() { local isReseedOrUpdate, cond1, cond2, isExtOrInt, enumRescanType localobj nil
        isReseedOrUpdate = $1
        
        cond1 = (isReseedOrUpdate && (mechManagerMainWidget != nil || gapJuncManagerMainWidget != nil || synManagerMainWidget != nil || ecsManagerMainWidget != nil))
        cond2 = (inhomAndStochLibrary.getNumInhomVarsForTaps() != 0)
        if (cond1 || cond2) {
            // Must not be called because we block the panel in Main UI
            codeContractViolation()
        }
        
        // !!! BUG: we don't reach this code when ManageAndRunWidget is nil (i.e. before the 1st opening)
        if (isAstrocyteOrNeuron) {
            for isExtOrInt = 0, 1 {
                gapJuncGroups[isExtOrInt].reCalcAndCacheDistInGapJuncs()
            }
        } else {
            synGroup.reCalcAndCacheDistInSyns()
        }
        
        inhomAndStochLibrary.onNanoReseedOrUpdate()
        dismissManagers()
        
        enumRescanType = 4 - isReseedOrUpdate
        mmIcrHelper.scheduleRescan(enumRescanType)
    }
    
    proc dismissHandler() {
        dismissManagers()
        // !! dismissIfNotNil(altRunControlWidget)
        mainBox.unmap()
    }
    
    // All next staff is private
    
    
    proc dismissManagers() {
        dismissIfNotNil(mechManagerMainWidget)
        dismissIfNotNil(gapJuncManagerMainWidget)
        dismissIfNotNil(synManagerMainWidget)
        dismissIfNotNil(ecsManagerMainWidget)
    }
    
endtemplate ManageAndRunWidget
