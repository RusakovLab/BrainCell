
begintemplate SynLocationWidget

    public show, getSynLocName, dismissHandler
    
    external pyObj
    external mth
    external createEmptyPanel, unmapIfNotNil, codeContractViolation
    external enumArgTypes
    
    objref mainBox, deck
    
    objref synSet
    
    enumSynLoc = -1
    randomSynLocP = -1
    isReviewAfterApply = -1
    
    pMin = -1
    pMax = -1
    
    
    proc init() {
        isReviewAfterApply = 0
        
        pMin = 0
        pMax = 1
        
        variable_domain(&randomSynLocP, pMin, pMax)
    }
    
    proc show() { local synLocIdx
        strdef synLocName, handlerHocCommand, text
        
        synSet = $o1
        
        enumSynLoc = synSet.enumSynLoc
        randomSynLocP = synSet.randomSynLocP
        
        mainBox = new VBox()
        mainBox.intercept(1)
        {
            xpanel("")
            xlabel("Synapse location:")
            for synLocIdx = 0, 2 {
                getSynLocName(synLocName, synLocIdx)
                sprint(handlerHocCommand, "synLocChangedHandler(%d)", synLocIdx)
                xradiobutton(synLocName, handlerHocCommand, enumSynLoc == synLocIdx)
            }
            xpanel()
            deck = new Deck()
            deck.intercept(1)
            {
                createEmptyPanel()
                xpanel("")
                xlabel("Bernoulli distribution:")
                for synLocIdx = 0, 1 {
                    getSynLocName(synLocName, synLocIdx)
                    sprint(text, "p = %d means \"all on %s\"", synLocIdx, synLocName)
                    xlabel(text)
                }
                xpvalue("p", &randomSynLocP, 1)
                xslider(&randomSynLocP, pMin, pMax)
                xpanel()
            }
            deck.intercept(0)
            // deck.flip_to(*)     // Will be done below in synLocChangedHandler
            deck.map()
            xpanel("")
            xlabel("Notes:")
            getSynLocName(synLocName, 1)
            sprint(text, "* When a synapse is moved to %s,", synLocName)
            xlabel(text)
            sprint(text, "  we set Spine Neck diam to %s (%s).", pyObj.str(synSet.witheredSpineNeckDiam), units(mth.diamVarName))
            xlabel(text)
            getSynLocName(synLocName, 0)
            sprint(text, "* When the synapse is moved back to %s,", synLocName)
            xlabel(text)
            xlabel("  we restore the old value of Spine Neck diam.")
            xbutton("Apply", "applyHandler()")
            xlabel("")
            xcheckbox("Review after \"Apply\"", &isReviewAfterApply)
            xlabel("       (this will open a new \"PointBrowser\")")
            xpanel()
        }
        mainBox.intercept(0)
        mainBox.dismiss_action("dismissHandler()")
        mainBox.map("Synapse location", 950, 65, -1, -1)
        
        synLocChangedHandler(enumSynLoc)
    }
    
    proc getSynLocName() { local argType, synLocIdx localobj _synSet
        argType = argtype(2)
        
        if (argType == enumArgTypes.number) {
            synLocIdx = $2
        } else if (argType == enumArgTypes.objref_) {
            _synSet = $o2
            synLocIdx = _synSet.enumSynLoc
        } else {
            codeContractViolation()
        }
        
        if (synLocIdx == 0) {
            $s1 = "Spine Head"
        } else if (synLocIdx == 1) {
            $s1 = "Dendrite (just under Spine Neck)"
        } else if (synLocIdx == 2) {
            $s1 = "Random"
        } else {
            codeContractViolation()
        }
    }
    
    proc dismissHandler() { localobj nil
        unmapIfNotNil(mainBox)
        
        synSet = nil
    }
    
    // All next staff is private
    
    
    proc synLocChangedHandler() { local cardIdx
        enumSynLoc = $1
        cardIdx = (enumSynLoc == 2)
        deck.flip_to(cardIdx)
    }
    
    proc applyHandler() {
        synSet.enumSynLoc = enumSynLoc
        if (enumSynLoc == 2) {
            synSet.randomSynLocP = randomSynLocP
        }
        synSet.applyChangesToLoc(isReviewAfterApply)
        isReviewAfterApply = 0
        dismissHandler()
    }
    
endtemplate SynLocationWidget

objref slw
slw = new SynLocationWidget()
