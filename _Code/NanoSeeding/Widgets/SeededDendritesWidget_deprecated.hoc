
// !! deprecated in favour of SectionsSelectorWidget
begintemplate SeededDendritesSelectionWidget

    public show, dismissHandler
    
    external dendrite_ref, seededDendrite_ref
    external mwh, nanoBranchesManager
    external checkIfCurrentSecIsDendrite, addThisAndAllChildSecRefsToTheList_deprecated, removeAllDupSecRefsFromTheList, concatenateTwoLists
    external eachSecInList
    external enumColours
    
    isSelectWholeSubtree = -1
    
    dendriteColor = -1
    notDendriteColor = -1
    selectedColor = -1
    
    strdef numSeededDendsHint
    
    objref mainBox, plotShape
    
    objref selectedDendrite_ref
    
    
    proc init() {
        isSelectWholeSubtree = 1
        
        dendriteColor = enumColours.blue
        notDendriteColor = enumColours.black
        selectedColor = enumColours.red
        
        selectedDendrite_ref = new List()
        concatenateTwoLists(selectedDendrite_ref, seededDendrite_ref)
    }
    
    proc show() {
        mainBox = new VBox()
        mainBox.intercept(1)
        {
            xpanel("")
            xlabel("Please select the dendrites that will be seeded with nanostructures.")
            xlabel("")
            xradiobutton("Select individual sections", "isSelectWholeSubtree = 0", !isSelectWholeSubtree)
            xradiobutton("Select whole subtrees", "isSelectWholeSubtree = 1", isSelectWholeSubtree)
            xlabel("")
            // Usage of "Ctrl" is more natural for multiselect than "Shift", but for some reason, mouseEventsHandler is not called at all when "Ctrl" is pressed
            xlabel("Hold \"Shift\" when clicking on sections to keep the previous selection.")
            xlabel("The sections shown in black are not dendrites, and so cannot be selected.")
            xpanel()
            plotShape = new PlotShape()
            colorizeCompartments()
            selectSections()
            plotShape.menu_tool("Seeded Dendrite(s)", "mouseEventsHandler")
            plotShape.exec_menu("Seeded Dendrite(s)")
            xpanel("")
            xvarlabel(numSeededDendsHint)
            xbutton("Done", "doneHandler()")
            xpanel()
        }
        mainBox.intercept(0)
        mainBox.dismiss_action("dismissHandler(0)")
        mainBox.map("Seeded dendrite(s)")
        plotShape.exec_menu("View = plot")
    }
    
    proc dismissHandler() { local isSkipReseeding
        isSkipReseeding = $1
        
        if (!isSkipReseeding) {
            // !! maybe need to inform user that we'll reseed rather than restore the old nanostructures
            nanoBranchesManager.reseedBranchesWithLastUsedArgs()
        }
        
        mainBox.unmap()
    }
    
    // All next staff is private
    
    
    proc colorizeCompartments() {
        plotShape.color_all(notDendriteColor)
        for eachSecInList(dendrite_ref) {
            plotShape.color(dendriteColor)
        }
    }
    
    proc selectSections() {
        if (selectedDendrite_ref.count() != 0) {
            showSelectedDendsAndUpdateHint()
        }
    }
    
    // Mouse events handler: section selection and highlighting
    // $1 - Event type: 1, 2, 3 means move, press, release respectively
    // $2 - x
    // $3 - y
    // $4 - Keystate: reflects the state of Control (bit 1), Shift (bit 2), and Meta (bit 3) keys
    proc mouseEventsHandler() { local eventType, x, y, keystate, isMultiselect, isDendrite
        eventType = $1
        x = $2
        y = $3
        keystate = $4
        
        if (eventType != 2) {   // LMB press
            return
        }
        
        isMultiselect = (keystate == 2) // Shift
        if (!isMultiselect) {
            selectedDendrite_ref.remove_all()
            colorizeCompartments()
        }
        
        // Make the nearest section currently accessed
        plotShape.nearest(x, y)
        plotShape.push_selected()
        
        // Check if the section belongs to a dendrite
        isDendrite = checkIfCurrentSecIsDendrite()
        if (!isDendrite) {
            pop_section()
            updateNumSeededDendsHint()
            return
        }
        
        if (!isSelectWholeSubtree) {
            selectedDendrite_ref.append(new SectionRef())
        } else {
            addThisAndAllChildSecRefsToTheList_deprecated(selectedDendrite_ref)
        }
        
        pop_section()
        
        if (isMultiselect) {
            removeAllDupSecRefsFromTheList(selectedDendrite_ref)
        }
        
        showSelectedDendsAndUpdateHint()
    }
    
    proc showSelectedDendsAndUpdateHint() {
        for eachSecInList(selectedDendrite_ref) {
            plotShape.color(selectedColor)
        }
        
        updateNumSeededDendsHint()
    }
    
    proc updateNumSeededDendsHint() {
        sprint(numSeededDendsHint, "%d dendrite section(s) selected", selectedDendrite_ref.count())
    }
    
    // The handler of "Done" button
    proc doneHandler() {
        if (selectedDendrite_ref.count() == 0) {
            mwh.showMessageBox("Please select at least 1 dendrite section.", "Nothing selected")
            return
        }
        
        // Here is the result of selection
        seededDendrite_ref = selectedDendrite_ref
        
        nanoBranchesManager.reseedBranchesWithLastUsedArgs()
        
        mainBox.unmap()
    }
    
endtemplate SeededDendritesSelectionWidget


objref sdsWidget

// Handler of "Reseed" button from the main UI
// Input: isAllOrSomeDendritesSeeded (taken from the top level)
proc seededDendritesChangedHandler() { localobj nil
    if (isAllOrSomeDendritesSeeded) {
        seededDendrite_ref = dendrite_ref
        nanoBranchesManager.reseedBranchesWithLastUsedArgs()
    } else {
        // !! after the next operation and before closing sdsWidget, interaction with MainUI nanogeometry panels causes an error,
        //    so maybe need to hide MainUI temporarily
        nanoBranchesManager.deleteBranches()
        
        if (sdsWidget != nil) {
            sdsWidget.dismissHandler(1)
        }
        sdsWidget = new SeededDendritesSelectionWidget()
        sdsWidget.show()
    }
}
